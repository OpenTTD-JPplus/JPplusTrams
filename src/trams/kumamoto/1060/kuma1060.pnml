
// https://ja.wikipedia.org/wiki/%E5%AE%87%E9%83%BD%E5%AE%AE%E3%83%A9%E3%82%A4%E3%83%88%E3%83%AC%E3%83%BC%E3%83%AB9200%E5%BD%A2%E9%9B%BB%E8%BB%8A


switch(FEAT_ROADVEHS, SELF, sw_kuma1060_subtype_text, cargo_subtype) {
	    0: return string(LV_IRL);
        1: return string(LV_1CC);
        return CB_RESULT_NO_TEXT;
        }

// Graphics

    // Purchase
    spriteset(spriteset_kuma1060_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1060/kuma1060_purchase.png") {tmpl_p()}

    switch(FEAT_ROADVEHS, SELF, sw_kuma1060_purchase, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: logo_kumamoto; 
            1: spriteset_kuma1060_purchase;
        }

	// Panto & Doors
	spriteset(panto_kuma1060, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1060/kuma1060_panto.png") {tmpl_8(1,1)}
	spriteset(doorz_kuma1060, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1060/kuma1060_doors.png") {tmpl_8(1,1)}

	spritegroup sg_doors_kuma1060 {
        loaded:  [sprite_blank];
        loading: [doorz_kuma1060];
        }

    switch(FEAT_ROADVEHS, SELF, doors_kuma1060, OpenDoors()) {1:sg_doors_kuma1060 ;}

    // Tram Graphics
    spriteset(spriteset_kuma1060_lead_irl,          ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1060/kuma1060_irl.png") {tmpl_8(1,1)}
    spriteset(spriteset_kuma1060_lead_1cc,          "src/trams/kumamoto/1060/kuma1060_1cc.png") {tmpl_8(1,1)}
    switch(FEAT_ROADVEHS, PARENT, sw_kuma1060_lead_1cc, cargo_subtype) {1:spriteset_kuma1060_lead_1cc;}

    switch(FEAT_ROADVEHS, SELF, sw_kuma1060_stack, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: spriteset_kuma1060_lead_irl; // Base
            1: sw_kuma1060_lead_1cc;        // 1cc mask
            2: panto_kuma1060;          	// Pantos
            3: doors_kuma1060;              // Doors
        }

item(FEAT_ROADVEHS, item_kuma1060) {
    property {
		name:							string(STR_KUMA1060);
		climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		loading_speed:					25;
		power:							51 * 2 hp;
		introduction_date:				date(1951,01,01);
		model_life:						10;
		vehicle_life:					60;
		reliability_decay:				14;
		tram_type:						ELRL;
		sound_effect:					SOUND_POWER_STATION;
		visual_effect:					VISUAL_EFFECT_ELECTRIC;
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
		refit_cost:						0; // refitting is free
		running_cost_base:				RUNNING_COST_ROADVEH;
		weight:							15.25 ton;
		length:							8;
        }
    graphics {
		additional_text:				string(STR_CONCAT_4, string(OPERATORS),string(KUMAMOTO),string(LIVERIES),string(IRL1CC));
		cargo_subtype_text:				sw_kuma1060_subtype_text;
		cargo_capacity:					(param_capacity * 74 ) / 2;
		purchase_cargo_capacity:		(param_capacity * 74 ) / 2;
		speed:							sw_speed_24;
		default:						sw_kuma1060_stack;
		cargo_age_period:				sw_cargo_age_period_01;
		running_cost_factor:			45 + ((param_runcost * 30 ) / 2 ) * RunningCostFactor();
		purchase_running_cost_factor:	45 + ((param_runcost * 30 ) / 2 );
		purchase:						sw_kuma1060_purchase;
		cost_factor:					((param_buycost * 90 ) / 4 );
        }
}