
// https://ja.wikipedia.org/wiki/%E5%AE%87%E9%83%BD%E5%AE%AE%E3%83%A9%E3%82%A4%E3%83%88%E3%83%AC%E3%83%BC%E3%83%AB9200%E5%BD%A2%E9%9B%BB%E8%BB%8A


switch(FEAT_ROADVEHS, SELF, sw_kuma1350_subtype_text, cargo_subtype) {
	    0: return string(LV_IRL);
        1: return string(LV_YELLOWBLUE);
        2: return string(LV_YELLOW);
        3: return string(LV_ORANGEWHITE);
        4: return string(LV_REDBLUE);
        5: return string(LV_RED);
        6: return string(LV_1CC);
        return CB_RESULT_NO_TEXT;
        }

// Graphics

    // Purchase
    spriteset(ss_kuma1350_purchase,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1350/kuma1350_purchase.png") {tmpl_p()}

    switch(FEAT_ROADVEHS, SELF, sw_kuma1350_purchase, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: logo_kumamoto; 
            1: ss_kuma1350_purchase;
        }

    // Tram Graphics
    spriteset(ss_kuma1350_lead_irl, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1350/kuma1350_irl.png") {tmpl_7(1,1)}
    spriteset(ss_kuma1350_lead_yab, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1350/kuma1350_yab.png") {tmpl_7(1,1)}
    spriteset(ss_kuma1350_lead_yel, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1350/kuma1350_yel.png") {tmpl_7(1,1)}
    spriteset(ss_kuma1350_lead_oaw, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1350/kuma1350_oaw.png") {tmpl_7(1,1)}
    spriteset(ss_kuma1350_lead_rab, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1350/kuma1350_rab.png") {tmpl_7(1,1)}
    spriteset(ss_kuma1350_lead_red, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/1350/kuma1350_red.png") {tmpl_7(1,1)}
    spriteset(ss_kuma1350_lead_1cc, "src/trams/kumamoto/1350/kuma1350_1cc.png") {tmpl_7(1,1)}
    
    switch(FEAT_ROADVEHS, PARENT, sw_kuma1350_lead_mask, cargo_subtype) {
        1:  ss_kuma1350_lead_yab;
        2:  ss_kuma1350_lead_yel;
        3:  ss_kuma1350_lead_oaw;
        4:  ss_kuma1350_lead_rab;
        5:  ss_kuma1350_lead_red;
        6:  ss_kuma1350_lead_1cc;
       ss_kuma1350_lead_irl;
        }

    switch(FEAT_ROADVEHS, SELF, sw_kuma1350_stack, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: ss_kuma1350_lead_irl;        // Base
            1: sw_kuma1350_lead_mask;        // 1cc mask
            2: sprite_panto02;              // Pantos
            3: door01;                      // Doors
        }

item(FEAT_ROADVEHS, item_kuma1350) {
    property {
		name:							string(STR_KUMA1350);
		climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		loading_speed:					25;
		speed:							24 km/h;
		power:							51 * 2 hp;
		introduction_date:				date(1960,01,01);
		model_life:						10;
		vehicle_life:					60;
		reliability_decay:				14;
		tram_type:						ELRL;
		sound_effect:					SOUND_POWER_STATION;
		visual_effect:					VISUAL_EFFECT_ELECTRIC;
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
		refit_cost:						0; // refitting is free
		running_cost_base:				RUNNING_COST_ROADVEH;
		weight:							15.25 ton;
		length:							7;
        }
    graphics {
		additional_text:				string(STR_CONCAT_4, string(OPERATORS),string(KUMAMOTO),string(LIVERIES),string(IRL1CC));
		cargo_subtype_text:				sw_kuma1350_subtype_text;
		cargo_capacity:					(param_capacity * 72 ) / 2;
		purchase_cargo_capacity:		(param_capacity * 72 ) / 2;
		default:						sw_kuma1350_stack;
		cargo_age_period:				sw_cargo_age_period_01;
		running_cost_factor:			40 + ((param_runcost * 28 ) / 2 ) * RunningCostFactor();
		purchase_running_cost_factor:	40 + ((param_runcost * 28 ) / 2 );
		purchase:						sw_kuma1350_purchase;
		cost_factor:					((param_buycost * 88 ) / 4 );
        }
}
