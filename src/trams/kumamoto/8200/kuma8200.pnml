
// Cargo Subtypes

switch(FEAT_ROADVEHS, SELF, sw_kuma8200_subtype_text, cargo_subtype) {
	    0: return string(LV_IRL);
        1: return string(LV_TURQ);
        2: return string(LV_1CC);
        return CB_RESULT_NO_TEXT;
        }

// Graphics

    // Purchase
    spriteset(ss_kuma8200_purchase,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/8200/kuma8200_purchase.png") {tmpl_p()}

    switch(FEAT_ROADVEHS, SELF, sw_kuma8200_purchase, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: logo_kumamoto; 
            1: ss_kuma8200_purchase;
        }

    // Tram Graphics
    spriteset(ss_kuma8200_lead_irl, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/8200/kuma8200_irl.png") {tmpl_7(1,1)}
    spriteset(ss_kuma8200_lead_trq, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/kumamoto/8200/kuma8200_trq.png") {tmpl_7(1,1)}
    spriteset(ss_kuma8200_lead_1cc, "src/trams/kumamoto/8200/kuma8200_1cc.png") {tmpl_7(1,1)}

    switch(FEAT_ROADVEHS, PARENT, sw_kuma8200_lead_1cc, cargo_subtype) {1:ss_kuma8200_lead_trq; 2:ss_kuma8200_lead_1cc;}

    switch(FEAT_ROADVEHS, SELF, sw_kuma8200_stack, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: ss_kuma8200_lead_irl; // Base
            1: sw_kuma8200_lead_1cc;        // Mask
            2: sprite_panto03;              // Pantos
            3: door01;                      // Doors
        }

item(FEAT_ROADVEHS, item_kuma8200) {
    property {
        name:                           string(STR_KUMA8200);
        climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        loading_speed:                  25;
        cargo_age_period:               240;
        cost_factor:                    175;
        running_cost_factor:            80;
        speed:                          40 km/h;    
        power:                          162 hp;  // 120kW per motor, 1 motor
        cargo_capacity:                 70;
        introduction_date:              date(1982,02,08);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   50;
        reliability_decay:              14;
        tram_type:                      ELRL;
        sound_effect:                   SOUND_POWER_STATION;
        visual_effect:                  VISUAL_EFFECT_ELECTRIC;
        sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
        misc_flags:                     bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:                     0; // Refitting is free
        running_cost_base:              RUNNING_COST_ROADVEH;
        weight:                         19 ton;
        length:                         7;
        }
    graphics {
        additional_text:                string(STR_CONCAT_4, string(OPERATORS),string(KUMAMOTO),string(LIVERIES),string(IRL1CC));
        cargo_subtype_text:             sw_kuma8200_subtype_text;
        cargo_capacity:                 (param_capacity * 70 ) / 2;
        purchase_cargo_capacity:        (param_capacity * 70 ) / 2;
        default:                        sw_kuma8200_stack;
        cargo_age_period:               sw_cargo_age_period_01;
        running_cost_factor:            45 + ((param_runcost * 30 ) / 2 ) * RunningCostFactor();
        purchase_running_cost_factor:   45 + ((param_runcost * 30 ) / 2 );
        purchase:                       sw_kuma8200_purchase;
        cost_factor:                    ((param_buycost*90)/4);
        }
}