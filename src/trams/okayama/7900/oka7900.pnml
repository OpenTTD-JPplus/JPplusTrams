
// Cargo Subtypes

    switch(FEAT_ROADVEHS, SELF, sw_oka7900_subtype_text, cargo_subtype) {
	    0: return string(LV_RANDOM);
        1: return string(LV_WHITE);
        2: return string(LV_YELLOW);
        3: return string(LV_YELLOWPINK);
        4: return string(LV_REDWHITE);
        5: return string(LV_BLUEWHITE);
        6: return string(LV_PINK);
        return CB_RESULT_NO_TEXT;
        }

// Graphics

    // Purchase
        spriteset(ss_oka7900_purchase,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7900/oka7900_purchase.png") {tmpl_p()}

        switch(FEAT_ROADVEHS, SELF, sw_oka7900_purchase, [ 
            STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
                0: logo_okayama; 
                1: ss_oka7900_purchase;
            }

    // Panto
        spriteset(panto_oka7900, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7900/oka7900_panto.png") {tmpl_8(1,1)}

    // Doors
        spriteset(doorz_oka7900, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7900/oka7900_doors.png") {tmpl_8(1,1)}

        spritegroup sg_doors_oka7900 {
            loaded:  [sprite_blank];
            loading: [doorz_oka7900];
            }

        switch(FEAT_ROADVEHS, SELF, doors_oka7900, OpenDoors()) {1:sg_doors_oka7900;}

    // Tram Graphics
        spriteset(ss_oka7900_lead_whi, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7900/oka7900_whi.png") {tmpl_8(1,1)}
        spriteset(ss_oka7900_lead_yel, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7900/oka7900_yel.png") {tmpl_8(1,1)}
        spriteset(ss_oka7900_lead_yap, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7900/oka7900_yap.png") {tmpl_8(1,1)}
        spriteset(ss_oka7900_lead_raw, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7900/oka7900_raw.png") {tmpl_8(1,1)}
        spriteset(ss_oka7900_lead_baw, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7900/oka7900_baw.png") {tmpl_8(1,1)}
        spriteset(ss_oka7900_lead_pnk, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7900/oka7900_pnk.png") {tmpl_8(1,1)}
    
        switch(FEAT_ROADVEHS, PARENT, sw_oka7900_lead_rdm, random_bits % 6) { 
            1: ss_oka7900_lead_yel;
            2: ss_oka7900_lead_yap;
            3: ss_oka7900_lead_raw;
            4: ss_oka7900_lead_baw;
            5: ss_oka7900_lead_pnk;
            ss_oka7900_lead_whi;
            }

        switch(FEAT_ROADVEHS, PARENT, ss_oka7900_lead_mask, cargo_subtype) {
            0: sw_oka7900_lead_rdm;
            2: ss_oka7900_lead_yel;
            3: ss_oka7900_lead_yap;
            4: ss_oka7900_lead_raw;
            5: ss_oka7900_lead_baw;
            6: ss_oka7900_lead_pnk;
            }

        switch(FEAT_ROADVEHS, SELF, sw_oka7900_stack, [ 
            STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
                0: ss_oka7900_lead_whi;             // Base
                1: ss_oka7900_lead_mask;            // Mask
                2: panto_oka7900;                   // Pantos
                3: doors_oka7900;                   // Doors
            }

item(FEAT_ROADVEHS, item_oka7900) {
    property {
		name:							string(STR_OKA7900);
		climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		loading_speed:					25;
		power:							51 * 2 hp;
		introduction_date:				date(1989,01,01);
		model_life:						10;
		vehicle_life:					60;
		reliability_decay:				14;
		tram_type:						ELRL;
		sound_effect:					SOUND_POWER_STATION;
		visual_effect:					VISUAL_EFFECT_ELECTRIC;
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
		refit_cost:						0; // refitting is free
		running_cost_base:				RUNNING_COST_ROADVEH;
		weight:							16.5 ton;
		length:							8;
        }
    graphics {
		additional_text:				string(STR_CONCAT_4, string(OPERATORS),string(OKAYAMA),string(LIVERIES),string(VARIOUS));
		cargo_subtype_text:				sw_oka7900_subtype_text;
		cargo_capacity:					(param_capacity * 96 ) / 2;
		purchase_cargo_capacity:		(param_capacity * 96 ) / 2;
		speed:							sw_speed_40;
		default:						sw_oka7900_stack;
		cargo_age_period:				sw_cargo_age_period_01;
		running_cost_factor:			44 + ((param_runcost * 29 ) / 2 ) * RunningCostFactor();
		purchase_running_cost_factor:	44 + ((param_runcost * 29 ) / 2 );
		purchase:						sw_oka7900_purchase;
		cost_factor:					((param_buycost * 89 ) / 4 );
        }
}
