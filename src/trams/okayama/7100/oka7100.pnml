
// Cargo Subtypes

    switch(FEAT_ROADVEHS, SELF, sw_oka7100_subtype_text, cargo_subtype) {
	    0: return string(LV_RANDOM);
        1: return string(LV_REDWHITE);
        2: return string(LV_ORANGE);
        3: return string(LV_GREENORANGE);
        4: return string(LV_WHITEGREENBLUE);
        5: return string(LV_REDYELLOW);
        6: return string(LV_RED);
        return CB_RESULT_NO_TEXT;
        }

// Graphics

    // Purchase
    spriteset(ss_oka7100_purchase,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7100/oka7100_purchase.png") {tmpl_p()}

    switch(FEAT_ROADVEHS, SELF, sw_oka7100_purchase, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: logo_okayama; 
            1: ss_oka7100_purchase;
        }

    // Tram Graphics
    spriteset(ss_oka7100_lead_whi, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7100/oka7100_whi.png") {tmpl_8(1,1)}
    spriteset(ss_oka7100_lead_raw, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7100/oka7100_raw.png") {tmpl_8(1,1)}
    spriteset(ss_oka7100_lead_ora, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7100/oka7100_ora.png") {tmpl_8(1,1)}
    spriteset(ss_oka7100_lead_wlb, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7100/oka7100_wlb.png") {tmpl_8(1,1)}
    spriteset(ss_oka7100_lead_ray, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7100/oka7100_ray.png") {tmpl_8(1,1)}
    spriteset(ss_oka7100_lead_red, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7100/oka7100_red.png") {tmpl_8(1,1)}
 
    switch(FEAT_ROADVEHS, PARENT, sw_oka7100_lead_rdm, random_bits % 6) { 
        1: ss_oka7100_lead_raw;
        2: ss_oka7100_lead_ora;
        3: ss_oka7100_lead_wlb;
        4: ss_oka7100_lead_ray;
        5: ss_oka7100_lead_red;
        ss_oka7100_lead_whi;
        }

    switch(FEAT_ROADVEHS, PARENT, ss_oka7100_lead_mask, cargo_subtype) {
        0: sw_oka7100_lead_rdm;
        2: ss_oka7100_lead_raw;
        3: ss_oka7100_lead_ora;
        4: ss_oka7100_lead_wlb;
        5: ss_oka7100_lead_ray;
        6: ss_oka7100_lead_red;
        }

    switch(FEAT_ROADVEHS, SELF, sw_oka7100_stack, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: ss_oka7100_lead_whi;             // Base
            1: ss_oka7100_lead_mask;            // Mask
            2: sprite_panto06;                  // Pantos
            3: door11;                          // Doors
        }

item(FEAT_ROADVEHS, item_oka7100) {
    property {
		name:							string(STR_OKA7100);
		climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		loading_speed:					25;
		power:							51 * 2 hp;
		introduction_date:				date(1981,01,01);
		model_life:						10;
		vehicle_life:					60;
		reliability_decay:				14;
		tram_type:						ELRL;
		sound_effect:					SOUND_POWER_STATION;
		visual_effect:					VISUAL_EFFECT_ELECTRIC;
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
		refit_cost:						0; // refitting is free
		running_cost_base:				RUNNING_COST_ROADVEH;
		weight:							16.5 ton;
		length:							8;
        }
    graphics {
		additional_text:				string(STR_CONCAT_4, string(OPERATORS),string(OKAYAMA),string(LIVERIES),string(VARIOUS));
		cargo_subtype_text:				sw_oka7100_subtype_text;
		cargo_capacity:					(param_capacity * 96 ) / 2;
		purchase_cargo_capacity:		(param_capacity * 96 ) / 2;
		speed:							sw_speed_40;
		default:						sw_oka7100_stack;
		cargo_age_period:				sw_cargo_age_period_01;
		running_cost_factor:			45 + ((param_runcost * 30 ) / 2 ) * RunningCostFactor();
		purchase_running_cost_factor:	45 + ((param_runcost * 30 ) / 2 );
		purchase:						sw_oka7100_purchase;
		cost_factor:					((param_buycost * 90 ) / 4 );
        }
}
