
// Cargo Subtypes

    switch(FEAT_ROADVEHS, SELF, sw_oka7500_subtype_text, cargo_subtype) {
	    0: return string(LV_RANDOM);
        1: return string(LV_YELLOWBLUE);
        2: return string(LV_BLUESTRIPE);
        3: return string(LV_REDSTRIPE);
        4: return string(LV_REDWHITE);
        return CB_RESULT_NO_TEXT;
        }

// Graphics

    // Purchase
        spriteset(ss_oka7500_purchase,     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7500/oka7500_purchase.png") {tmpl_p()}

        switch(FEAT_ROADVEHS, SELF, sw_oka7500_purchase, [ 
            STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
                0: logo_okayama; 
                1: ss_oka7500_purchase;
            }

    // Panto
        spriteset(panto_oka7500, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7500/oka7500_panto.png") {tmpl_8(1,1)}

    // Doors
        spriteset(doorz_oka7500, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7500/oka7500_doors.png") {tmpl_8(1,1)}

        spritegroup sg_doors_oka7500 {
            loaded:  [sprite_blank];
            loading: [doorz_oka7500];
            }

        switch(FEAT_ROADVEHS, SELF, doors_oka7500, OpenDoors()) {1:sg_doors_oka7500;}

    // Tram Graphics
        spriteset(ss_oka7500_lead_whi, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7500/oka7500_whi.png") {tmpl_8(1,1)}
        spriteset(ss_oka7500_lead_yab, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7500/oka7500_yab.png") {tmpl_8(1,1)}
        spriteset(ss_oka7500_lead_bls, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7500/oka7500_bls.png") {tmpl_8(1,1)}
        spriteset(ss_oka7500_lead_rds, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7500/oka7500_rds.png") {tmpl_8(1,1)}
        spriteset(ss_oka7500_lead_raw, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/okayama/7500/oka7500_raw.png") {tmpl_8(1,1)}
    
        switch(FEAT_ROADVEHS, PARENT, sw_oka7500_lead_rdm, random_bits % 4) { 
            0: ss_oka7500_lead_yab;
            1: ss_oka7500_lead_bls;
            2: ss_oka7500_lead_rds;
            3: ss_oka7500_lead_raw;
            }

        switch(FEAT_ROADVEHS, PARENT, ss_oka7500_lead_mask, cargo_subtype) {
            0: sw_oka7500_lead_rdm;
            1: ss_oka7500_lead_yab;
            2: ss_oka7500_lead_bls;
            3: ss_oka7500_lead_rds;
            4: ss_oka7500_lead_raw;
            }

        switch(FEAT_ROADVEHS, SELF, sw_oka7500_stack, [ 
            STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
                0: ss_oka7500_lead_whi;             // Base
                1: ss_oka7500_lead_mask;            // Mask
                2: panto_oka7500;                   // Pantos
                3: doors_oka7500;                   // Doors
            }

item(FEAT_ROADVEHS, item_oka7500) {
    property {
		name:							string(STR_OKA7500);
		climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		loading_speed:					25;
		power:							51 * 2 hp;
		introduction_date:				date(1985,01,01);
		model_life:						10;
		vehicle_life:					60;
		reliability_decay:				14;
		tram_type:						ELRL;
		sound_effect:					SOUND_POWER_STATION;
		visual_effect:					VISUAL_EFFECT_ELECTRIC;
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
		refit_cost:						0; // refitting is free
		running_cost_base:				RUNNING_COST_ROADVEH;
		weight:							16.5 ton;
		length:							8;
        }
    graphics {
		additional_text:				string(STR_CONCAT_4, string(OPERATORS),string(OKAYAMA),string(LIVERIES),string(VARIOUS));
		cargo_subtype_text:				sw_oka7500_subtype_text;
		cargo_capacity:					(param_capacity * 96 ) / 2;
		purchase_cargo_capacity:		(param_capacity * 96 ) / 2;
		speed:							sw_speed_40;
		default:						sw_oka7500_stack;
		cargo_age_period:				sw_cargo_age_period_01;
		running_cost_factor:			45 + ((param_runcost * 30 ) / 2 ) * RunningCostFactor();
		purchase_running_cost_factor:	45 + ((param_runcost * 30 ) / 2 );
		purchase:						sw_oka7500_purchase;
		cost_factor:					((param_buycost * 90 ) / 4 );
        }
}
