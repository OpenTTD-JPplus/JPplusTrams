
// https://ja.wikipedia.org/wiki/%E5%AE%87%E9%83%BD%E5%AE%AE%E3%83%A9%E3%82%A4%E3%83%88%E3%83%AC%E3%83%BC%E3%83%AB9200%E5%BD%A2%E9%9B%BB%E8%BB%8A


switch(FEAT_ROADVEHS, SELF, sw_mlrv1000_subtype_text, cargo_subtype) {
	    0: return string(LV_IRL);
        1: return string(LV_DORAEMON);
        2: return string(LV_1CC);
        return CB_RESULT_NO_TEXT;
        }

// Graphics

    // Purchase
    spriteset(spriteset_mlrv1000_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/manyosen/mlrv1000/mlrv1000_purchase.png") {tmpl_p()}

    switch(FEAT_ROADVEHS, SELF, sw_mlrv1000_purchase, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: logo_manyosen; 
            1: spriteset_mlrv1000_purchase;
        }

    // Tram Graphics

    // Base
    spriteset(spriteset_mlrv1000_lead_irl, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/manyosen/mlrv1000/mlrv1000_irl.png") {tmpl_7(1,1 )}
    spriteset(spriteset_mlrv1000_rear_irl, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/manyosen/mlrv1000/mlrv1000_irl.png") {tmpl_7(1,25)}

    // Liveries
    spriteset(spriteset_mlrv1000_lead_doraemon, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/manyosen/mlrv1000/mlrv1000_doraemon.png") {tmpl_7(1,1 )}
    spriteset(spriteset_mlrv1000_rear_doraemon, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/manyosen/mlrv1000/mlrv1000_doraemon.png") {tmpl_7(1,25)}

    spriteset(spriteset_mlrv1000_lead_1cc, "src/trams/manyosen/mlrv1000/mlrv1000_1cc.png") {tmpl_7(1,1 )}
    spriteset(spriteset_mlrv1000_rear_1cc, "src/trams/manyosen/mlrv1000/mlrv1000_1cc.png") {tmpl_7(1,25)}

    // Mask Selection
    switch(FEAT_ROADVEHS, PARENT, sw_mlrv1000_lead_mask, cargo_subtype) {1:spriteset_mlrv1000_lead_doraemon; 2:spriteset_mlrv1000_lead_1cc;}
    switch(FEAT_ROADVEHS, PARENT, sw_mlrv1000_rear_mask, cargo_subtype) {1:spriteset_mlrv1000_rear_doraemon; 2:spriteset_mlrv1000_rear_1cc;}

    switch(FEAT_ROADVEHS, PARENT, sw_door_mlrv1000_lead_doraemon_check, cargo_subtype) {1:door_doraemon_lead;}
    switch(FEAT_ROADVEHS, PARENT, sw_door_mlrv1000_rear_doraemon_check, cargo_subtype) {1:door_doraemon_rear;}

    switch(FEAT_ROADVEHS, SELF, sw_mlrv1000_lead_stack, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: spriteset_mlrv1000_lead_irl;             // Base
            1: sw_mlrv1000_lead_mask;                   // Mask
            2: door02_lead;                             // Doors (base)
            3: sw_door_mlrv1000_lead_doraemon_check;    // Doors (extra layer for Doraemon)
            4: sprite_panto01;                          // Pantos
        }

    switch(FEAT_ROADVEHS, SELF, sw_mlrv1000_rear_stack, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: spriteset_mlrv1000_rear_irl;             // Base
            1: sw_mlrv1000_rear_mask;                   // Mask
            2: door02_rear;                             // Doors (base)
            3: sw_door_mlrv1000_rear_doraemon_check;    // Doors (extra layer for Doraemon)
        }

        switch (FEAT_ROADVEHS, SELF, sw_mlrv1000_stack, position_in_consist ) {
            0:  sw_mlrv1000_lead_stack;
            sw_mlrv1000_rear_stack;                      //default
        }

switch (FEAT_ROADVEHS, SELF, sw_mlrv1000_spark, position_in_consist ) {
    0:  return VISUAL_EFFECT_DISABLE;
    1:  return VISUAL_EFFECT_ELECTRIC;
        return VISUAL_EFFECT_DISABLE; //default
}

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, sw_mlrv1000_articulated_part, extra_callback_info1) {
    0..1:  return item_mlrv1000;
        return CB_RESULT_NO_MORE_ARTICULATED_PARTS; //stop adding vehicle parts
}

switch (FEAT_ROADVEHS, SELF, sw_mlrv1000_cargo_age_period, (param_aging)) {
    0:  return 240;
    1:  return 200;
    2:  return 170;
    3:  return 140;
        return 240; //default
} 

item(FEAT_ROADVEHS, item_mlrv1000) {
    property {
        name:                           string(STR_MLRV1000);
        climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        loading_speed:                  25;
        cargo_age_period:               240;
        cost_factor:                    175;
        running_cost_factor:            80;
        speed:                          70 km/h;    // Current operates at 40km/h, but capable (with approval) of 70 km/h
        power:                          135 * 2 hp;
        cargo_capacity:                 74;
        introduction_date:              date(2004,01,21);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   50;
        reliability_decay:              14;
        tram_type:                      ELRL;
        sound_effect:                   SOUND_POWER_STATION;
        visual_effect:                  VISUAL_EFFECT_ELECTRIC;
        sprite_id:                      SPRITE_ID_NEW_ROADVEH; //enable new graphics
        misc_flags:                     bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:                     0; // Refitting is free
        running_cost_base:              RUNNING_COST_ROADVEH;
        weight:                         21 ton;
        }
    graphics {
        additional_text:                string(STR_CONCAT_4, string(OPERATORS),string(MANYOSEN),string(LIVERIES),string(DES_MLRV1000));
        cargo_subtype_text:             sw_mlrv1000_subtype_text;
        cargo_capacity:                 (param_capacity * 80 / num_vehs_in_consist) / 2;
        purchase_cargo_capacity:        (param_capacity * 80 / 2) / 2;
        default:                        sw_mlrv1000_stack;
        length:                         7;
        cargo_age_period:               sw_mlrv1000_cargo_age_period;
        running_cost_factor:            88 + ((param_runcost * 30 ) / 2 ) * RunningCostFactor();
        purchase_running_cost_factor:   88 + ((param_runcost * 30 ) / 2 );
        articulated_part:               sw_mlrv1000_articulated_part;
        visual_effect:                  sw_mlrv1000_spark;
        purchase:                       sw_mlrv1000_purchase;
        cost_factor:                    ((param_buycost*175)/4);
        }
}