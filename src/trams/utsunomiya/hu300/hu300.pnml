
// https://ja.wikipedia.org/wiki/%E5%AE%87%E9%83%BD%E5%AE%AE%E3%83%A9%E3%82%A4%E3%83%88%E3%83%AC%E3%83%BC%E3%83%ABHU300%E5%BD%A2%E9%9B%BB%E8%BB%8A


switch(FEAT_ROADVEHS, SELF, sw_hu300_subtype_text, cargo_subtype) {
	    0: return string(LV_IRL);
        1: return string(LV_1CC);
        2: return string(LV_2CC);
        return CB_RESULT_NO_TEXT;
        }

// Graphics

    // Purchase
    spriteset(spriteset_hu300_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/utsunomiya/hu300/hu300_purchase.png") {tmpl_p()}

    switch(FEAT_ROADVEHS, SELF, sw_hu300_purchase, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: logo_lightline; 
            1: spriteset_hu300_purchase;
        }

    // Panto
        spriteset(panto_hu300, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/utsunomiya/hu300/hu300_panto.png") {tmpl_7(1,1)}

    // Doors
        spriteset(doorz_hu300_lead, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/utsunomiya/hu300/hu300_doors.png") {tmpl_7(1,1)}
        spriteset(doorz_hu300_cntr, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/utsunomiya/hu300/hu300_doors.png") {tmpl_7(1,25)}
        spriteset(doorz_hu300_rear, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/utsunomiya/hu300/hu300_doors.png") {tmpl_7(1,49)}
            
        spritegroup sg_doors_hu300_lead {
            loaded:  [sprite_blank];
            loading: [doorz_hu300_lead];
        }

        spritegroup sg_doors_hu300_cntr {
            loaded:  [sprite_blank];
            loading: [doorz_hu300_cntr];
        }

        spritegroup sg_doors_hu300_rear {
            loaded:  [sprite_blank];
            loading: [doorz_hu300_rear];
        }

        switch(FEAT_ROADVEHS, SELF, doors_hu300_lead, OpenDoors()) {1:sg_doors_hu300_lead;}
        switch(FEAT_ROADVEHS, SELF, doors_hu300_cntr, OpenDoors()) {1:sg_doors_hu300_cntr;}
        switch(FEAT_ROADVEHS, SELF, doors_hu300_rear, OpenDoors()) {1:sg_doors_hu300_rear;}

    // Doors 2cc
        spriteset(doorz_hu300_lead_2cc, "src/trams/utsunomiya/hu300/hu300_doors_2cc.png") {tmpl_7(1,1)}
        spriteset(doorz_hu300_cntr_2cc, "src/trams/utsunomiya/hu300/hu300_doors_2cc.png") {tmpl_7(1,25)}
        spriteset(doorz_hu300_rear_2cc, "src/trams/utsunomiya/hu300/hu300_doors_2cc.png") {tmpl_7(1,49)}
            
        spritegroup sg_doors_hu300_lead_2cc {
            loaded:  [sprite_blank];
            loading: [doorz_hu300_lead_2cc];
        }

        spritegroup sg_doors_hu300_cntr_2cc {
            loaded:  [sprite_blank];
            loading: [doorz_hu300_cntr_2cc];
        }

        spritegroup sg_doors_hu300_rear_2cc {
            loaded:  [sprite_blank];
            loading: [doorz_hu300_rear_2cc];
        }

        switch(FEAT_ROADVEHS, SELF, doors_hu300_lead_2cc, OpenDoors()) {1:sg_doors_hu300_lead_2cc;}
        switch(FEAT_ROADVEHS, SELF, doors_hu300_cntr_2cc, OpenDoors()) {1:sg_doors_hu300_cntr_2cc;}
        switch(FEAT_ROADVEHS, SELF, doors_hu300_rear_2cc, OpenDoors()) {1:sg_doors_hu300_rear_2cc;}

    // Tram Graphics

        // Base
        spriteset(spriteset_hu300_lead_irl, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/utsunomiya/hu300/hu300_irl.png") {tmpl_7(1,1)}
        spriteset(spriteset_hu300_cntr_irl, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/utsunomiya/hu300/hu300_irl.png") {tmpl_7(1,25)}
        spriteset(spriteset_hu300_rear_irl, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "src/trams/utsunomiya/hu300/hu300_irl.png") {tmpl_7(1,49)}

        // Mask 1cc
        spriteset(spriteset_hu300_lead_1cc, "src/trams/utsunomiya/hu300/hu300_1cc.png") {tmpl_7(1,1)}
        spriteset(spriteset_hu300_cntr_1cc, "src/trams/utsunomiya/hu300/hu300_1cc.png") {tmpl_7(1,25)}
        spriteset(spriteset_hu300_rear_1cc, "src/trams/utsunomiya/hu300/hu300_1cc.png") {tmpl_7(1,49)}

        // Mask 2cc
        spriteset(spriteset_hu300_lead_2cc, "src/trams/utsunomiya/hu300/hu300_2cc.png") {tmpl_7(1,1)}
        spriteset(spriteset_hu300_cntr_2cc, "src/trams/utsunomiya/hu300/hu300_2cc.png") {tmpl_7(1,25)}
        spriteset(spriteset_hu300_rear_2cc, "src/trams/utsunomiya/hu300/hu300_2cc.png") {tmpl_7(1,49)}

        // Apply Mask?
        switch(FEAT_ROADVEHS, PARENT, sw_hu300_lead_mask, cargo_subtype) {1:spriteset_hu300_lead_1cc; 2:spriteset_hu300_lead_2cc;}
        switch(FEAT_ROADVEHS, PARENT, sw_hu300_cntr_mask, cargo_subtype) {1:spriteset_hu300_cntr_1cc; 2:spriteset_hu300_cntr_2cc;}
        switch(FEAT_ROADVEHS, PARENT, sw_hu300_rear_mask, cargo_subtype) {1:spriteset_hu300_rear_1cc; 2:spriteset_hu300_rear_2cc;}

        // Doors
        switch(FEAT_ROADVEHS, PARENT, doors_hu300_lead_2cc_check, cargo_subtype) {2:doors_hu300_lead_2cc;}
        switch(FEAT_ROADVEHS, PARENT, doors_hu300_cntr_2cc_check, cargo_subtype) {2:doors_hu300_cntr_2cc;}
        switch(FEAT_ROADVEHS, PARENT, doors_hu300_rear_2cc_check, cargo_subtype) {2:doors_hu300_rear_2cc;}

        switch(FEAT_ROADVEHS, SELF, sw_hu300_lead_stack, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: spriteset_hu300_lead_irl;        // Base
            1: sw_hu300_lead_mask;              // Mask
            2: doors_hu300_lead;                // Doors (base)
            3: doors_hu300_lead_2cc_check;      // Doors (extra layer for 2cc)
            4: panto_hu300;                     // Pantos
        }

        switch(FEAT_ROADVEHS, SELF, sw_hu300_cntr_stack, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: spriteset_hu300_cntr_irl;        // Base
            1: sw_hu300_cntr_mask;              // Mask
            2: doors_hu300_cntr;                // Doors (base)
            3: doors_hu300_cntr_2cc_check;      // Doors (extra layer for 2cc)
        }

        switch(FEAT_ROADVEHS, SELF, sw_hu300_rear_stack, [ 
        STORE_TEMP(CB_FLAG_MORE_SPRITES | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ]) { 
            0: spriteset_hu300_rear_irl;        // Base
            1: sw_hu300_rear_mask;              // Mask
            2: doors_hu300_rear;                // Doors (base)
            3: doors_hu300_rear_2cc_check;      // Doors (extra layer for 2cc)
        }

        switch (FEAT_ROADVEHS, SELF, sw_hu300_stack, position_in_consist ) {
            0:  sw_hu300_lead_stack;
            1:  sw_hu300_cntr_stack;
                sw_hu300_rear_stack; //default
        }

// Length of parts
switch (FEAT_ROADVEHS, SELF, sw_hu300_length, position_in_consist ) {
    1:  return 6;
    return 7; 
}

//set number of articulated parts
switch (FEAT_ROADVEHS, SELF, sw_hu300_articulated_part, extra_callback_info1) {
    0..2:  return item_hu300;
        return CB_RESULT_NO_MORE_ARTICULATED_PARTS; //stop adding vehicle parts
}

item(FEAT_ROADVEHS, item_hu300) {
    property {
		name:							string(STR_HU300);
		climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
		refittable_cargo_classes:		bitmask(CC_PASSENGERS);
		loading_speed:					25;
		power:							135 * 4 hp;
		introduction_date:				date(2023,08,23);
		model_life:						VEHICLE_NEVER_EXPIRES;
		vehicle_life:					60;
		reliability_decay:				14;
		tram_type:						ELRL;
		sound_effect:					SOUND_POWER_STATION;
		sprite_id:						SPRITE_ID_NEW_ROADVEH;
		misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
		refit_cost:						0; // refitting is free
		running_cost_base:				RUNNING_COST_ROADVEH;
		weight:							39 ton;
        }
    graphics {
		additional_text:				string(STR_CONCAT_4, string(OPERATORS),string(UTSUNOMIYA),string(LIVERIES),string(IRL1CC2CC));
		cargo_subtype_text:				sw_hu300_subtype_text;
		cargo_capacity:					(param_capacity * 160 / 3 ) / 2;
		purchase_cargo_capacity:		(param_capacity * 160 / 3 ) / 2;
		speed:							sw_speed_70;
		default:						sw_hu300_stack;
		length:							sw_hu300_length;
		cargo_age_period:				sw_cargo_age_period_01;
		running_cost_factor:			133 + ((param_runcost * 30 ) / 2 ) * RunningCostFactor();
		purchase_running_cost_factor:	133 + ((param_runcost * 30 ) / 2 );
		articulated_part:				sw_hu300_articulated_part;
		visual_effect:					sw_spark_on_1;
		purchase:						sw_hu300_purchase;
		cost_factor:					((param_buycost * 175 ) / 4 );
        }
}